plugins {
    id 'com.github.ben-manes.versions' version '0.17.0'
    id 'java-library'
    id 'java-gradle-plugin'
}

group = 'com.github.am4dr'
version = '0.1.0-SNAPSHOT'

ext {
    jdkVersion = '1.9'
    junit_version = '5.1.0'
    libraries = [
            'junit_api': [
                    'org.junit.jupiter:junit-jupiter-api:'+junit_version,
            ],
            'junit_runtime': [
                    'org.junit.jupiter:junit-jupiter-engine:'+junit_version,
            ]
    ]
    targetJarNames = ['unnamed', 'autonamed', 'named']
}

allprojects {
    repositories {
        jcenter()
    }
}

dependencies {
    implementation gradleApi()

    testImplementation gradleTestKit()
    testImplementation libraries.junit_api
    testRuntime libraries.junit_runtime
}

project(':targetJars') {
    apply plugin: 'java-library'

    def jarTaskNamer = { name -> "${name}Jar" }
    targetJarNames.each { String name ->
        sourceSets.create(name) {}
        configurations.create(name)
        def jarTaskName = jarTaskNamer(name)
        def jarTask = tasks.create(jarTaskName, Jar) { Jar jar ->
            jar.baseName = name
            jar.from sourceSets.getByName(name).output
        }
        artifacts.add(name, jarTask)
    }
    tasks.getByName(jarTaskNamer("autonamed")) { Jar task ->
        task.manifest { m ->
            m.attributes.put('Automatic-Module-Name', 'sample.autonamed')
        }
    }
}

sourceSets {
    test {
        resources {
            srcDirs = ['src/test/resources', 'targetJars/build/libs']
        }
    }
}
processTestResources {
    dependsOn(targetJarNames.collect { name -> ":targetJars:${name}Jar" })
}
test {
    useJUnitPlatform()
}
