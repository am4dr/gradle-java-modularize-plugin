plugins {
    id 'com.github.ben-manes.versions' version '0.17.0'
    id 'java-gradle-plugin'
    id 'maven'
    id "com.gradle.plugin-publish" version "0.9.10"
    id "com.jfrog.bintray" version "1.7.3"
}

group = 'com.github.am4dr.gradle'
version = '0.5.0-SNAPSHOT'

ext {
    jdkVersion = JavaVersion.VERSION_1_9
    junit_version = '5.1.0'
    libraries = [
            'junit_api': [
                    'org.junit.jupiter:junit-jupiter-api:'+junit_version,
            ],
            'junit_runtime': [
                    'org.junit.jupiter:junit-jupiter-engine:'+junit_version,
            ]
    ]
    standaloneTargetNames = ['unnamed', 'autonamed', 'named']
    testTargetsVersion = '2.0'
}

allprojects {
    apply plugin: 'java-library'

    sourceCompatibility = jdkVersion
    targetCompatibility = jdkVersion

    repositories {
        mavenLocal()
        jcenter()
    }

    dependencies {
        testImplementation libraries.junit_api
        testRuntime libraries.junit_runtime
    }

    test {
        useJUnitPlatform()
    }
}

project(':standalones') {
    apply plugin: 'maven'

    group = "com.github.am4dr.gradle.java-modularize-plugin"
    version = testTargetsVersion
    archivesBaseName = "test-target-sample"

    def jarTaskNamer = { name -> "${name}Jar" }
    standaloneTargetNames.each { String name ->
        sourceSets.create(name)
        def jarTask = tasks.create(jarTaskNamer(name), Jar) {
            from sourceSets.getByName(name).output
            classifier = name
        }
        configurations.create(name)
        artifacts.add(name, jarTask)
        artifacts.add("archives", jarTask)
    }
    tasks.getByName(jarTaskNamer("autonamed")) {
        manifest {
            attributes.put('Automatic-Module-Name', 'sample.autonamed')
        }
    }
}
project(':dependents') {
    apply plugin: 'maven'

    group = "com.github.am4dr.gradle.java-modularize-plugin"
    version = testTargetsVersion
    archivesBaseName = "test-dependent-target-sample"

    dependencies {
        api "$group:test-target-sample:$testTargetsVersion:unnamed"
    }
    compileJava {
        dependsOn ":standalones:install"
    }
}
project(':testTargets') {
    group = "com.github.am4dr.gradle.java-modularize-plugin"
    version = testTargetsVersion
    archivesBaseName = "test-targets"

    addTestTargetSamplesToTestResources(it)
}

project(':tooling') {
    apply plugin: 'maven'
    apply plugin: 'com.jfrog.bintray'

    version = "0.1.0"
    group = "com.github.am4dr.gradle.java-modularize-plugin"
    archivesBaseName = "tooling"

    dependencies {
        testImplementation project(':testTargets')
    }

    addTestTargetSamplesToTestResources(it)
    configureBintrayUpload(it, 'gradle-java-modularize-plugin-tooling')
}

dependencies {
    api project(':tooling')
    implementation gradleApi()

    testImplementation project(':testTargets')
    testImplementation gradleTestKit()
}
addTestTargetSamplesToTestResources(project)
configureBintrayUpload(project, 'gradle-java-modularize-plugin')

test {
    dependsOn ":standalones:install", ":dependents:install"
}

pluginBundle {
    website = 'https://github.com/am4dr/gradle-java-modularize-plugin'
    vcsUrl = 'https://github.com/am4dr/gradle-java-modularize-plugin.git'
    description = 'module-info.class generation and injection'
    tags = ['jigsaw', 'modules', 'jlink']

    plugins {
        modularizePlugin {
            id = 'com.github.am4dr.java-modularize'
            displayName = 'Gradle Java Modularize Plugin'
        }
    }
}

def addTestTargetSamplesToTestResources(Project project) {
    project.with {
        sourceSets {
            test {
                resources {
                    srcDirs += [':standalones', ':dependents'].collect { new File(project.project(it).buildDir, 'libs') }
                }
            }
        }
        processTestResources {
            dependsOn(standaloneTargetNames.collect { name -> ":standalones:${name}Jar" })
            dependsOn ':dependents:jar'
        }
    }
}

def configureBintrayUpload(Project project, String bintrayPackage) {
    project.with {
        javadoc {
            options {
                links 'https://docs.gradle.org/4.6/javadoc'
                links 'https://docs.oracle.com/javase/9/docs/api'
                locale 'en_US'
                addBooleanOption('html5', true)
            }
        }
        tasks.create('sourceJar', Jar) {
            from sourceSets.main.allSource
            classifier = 'sources'
        }
        tasks.create('javadocJar', Jar) {
            from javadoc
            classifier = 'javadoc'
        }
        configurations {
            jarForBintrayUpload
        }
        artifacts {
            jarForBintrayUpload jar
            jarForBintrayUpload sourceJar
            jarForBintrayUpload javadocJar
        }
        bintray {
            user = bintrayUsername
            key = bintrayApiKey
            pkg {
                repo = 'maven'
                name = bintrayPackage
                licenses = ['MIT']
                vcsUrl = 'https://github.com/am4dr/gradle-java-modularize-plugin.git'
                websiteUrl = 'https://github.com/am4dr/gradle-java-modularize-plugin'
                githubRepo = 'am4dr/gradle-java-modularize-plugin'
                version {
                    name = project.version
                    vcsTag = project.version
                    released  = new Date()
                }
            }
            configurations = ['jarForBintrayUpload']
        }
    }
}
